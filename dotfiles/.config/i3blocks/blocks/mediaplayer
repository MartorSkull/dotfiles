#!/usr/bin/env perl
# Copyright (C) 2014 Tony Crisci <tony@dubstepdish.com>
# Copyright (C) 2015 Thiago Perrotta <perrotta dot thiago at poli dot ufrj dot br>

# This program is free software: you can redistribute it and/or modify
# it under the terms of the GNU General Public License as published by
# the Free Software Foundation, either version 3 of the License, or
# (at your option) any later version.

# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.

# You should have received a copy of the GNU General Public License
# along with this program.  If not, see <http://www.gnu.org/licenses/>.

# Requires playerctl binary to be in your path (except cmus)
# See: https://github.com/acrisci/playerctl

use Time::HiRes qw(usleep);
use Env qw();

$player = "";
my $cache_file = $ENV{MEDIAPLAYER_CACHE_FILE}
              // "/tmp/mediainfo_" . $ENV{USER};

sub get_default_player {
    my @player_list = get_player_list();

    if (scalar @player_list > 0) {
        return $player_list[0];
    }
}

sub read_cached_player {
    open(my $fh, '<', $cache_file) or return "";
    my $cached_player = <$fh>;
    close $fh;
    chomp $cached_player;
    return $cached_player;
}

sub get_player_list {
    my $players = qx(playerctl -l 2> /dev/null);
    chomp $players;
    my @player_list = split("\n", $players);
    return @player_list;
}

sub get_player {
    if ($player) {
        return $player;
    }

    my $cached_player = read_cached_player();
    if ($cached_player and grep { $_ eq $cached_player } get_player_list()) {
        $player = $cached_player;
        set_player($cached_player);
        return $cached_player;
    }

    my $default_player = get_default_player();
    set_player($default_player);
    $player = $default_player;
    return $default_player;
}

sub set_player {
    my ($a1) = @_;
    open(my $fh, '>', $cache_file)
        or die "Could not open file '$cache_file' $!";
    print $fh $a1;
    close $fh;
    $player = $a1;
}

sub select_next_player {
    my @player_list = get_player_list();
    my $player = get_player();

    for (my $i = 0; $i < scalar @player_list; $i++) {
        if ($player_list[$i] eq $player) {
            my $next_index = ($i + 1) % scalar @player_list;
            set_player($player_list[$next_index]);
            return;
        }
    }
}

sub select_prev_player {
    my @player_list = get_player_list();
    my $cached_player = get_player();

    for (my $i = 0; $i < scalar @player_list; $i++) {
        if ($player_list[$i] eq $cached_player) {
            my $prev_index = ($i - 1 + scalar @player_list) % scalar @player_list;
            set_player($player_list[$prev_index]);
            return;
        }
    }
}

sub buttons {
    my $player_arg = player_arg();
    if ($ENV{'BLOCK_BUTTON'} == 1) {
        system("playerctl $player_arg previous");
    } elsif ($ENV{'BLOCK_BUTTON'} == 2) {
        system("playerctl $player_arg play-pause");
    } elsif ($ENV{'BLOCK_BUTTON'} == 3) {
        system("playerctl $player_arg next");
    } elsif ($ENV{'BLOCK_BUTTON'} == 4) {
        select_next_player();
    } elsif ($ENV{'BLOCK_BUTTON'} == 5) {
        select_prev_player();
    }
}

sub player_arg {
    my $player_name = get_player();
    if ($player_name) {
        return "--player=" . $player_name;
    } else {
        return "";
    }
}

sub playerctl {
    buttons();
    my $cached_player = get_player();
    $cached_player =~ s/\..*$//;
    $cached_player = get_player_list() > 1 ? " [" . $cached_player . "]" : " ";

    my $player_arg = player_arg();

    my $artist = qx(playerctl $player_arg metadata artist 2> /dev/null);
    chomp $artist;

    my $title = substr qx(playerctl $player_arg metadata title), 0 , -1;

    my @metadata = ();
    if ($? == 0) {
        push(@metadata, $artist) if $artist;
        push(@metadata, $title) if $title;
    } else {
        push(@metadata, "Nothing playing");
    }

    my $status = substr qx(playerctl $player_arg status), 0, -1;
    if ($status eq "Playing") {
        $status = "";
    } elsif ($status eq "Paused"){ 
        $status = "";
    } elsif ($status eq "Stopped"){ 
        $status = "";
    }

    print(join(" - ", @metadata) . $cached_player . $status) if @metadata;
}

playerctl;

print("\n");
